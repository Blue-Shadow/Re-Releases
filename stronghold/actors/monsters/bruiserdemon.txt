ACTOR BruiserDemon : StrongholdBaronOfHell 3333
{
	Species "BruiserDemon"
	//$Category Stronghold_Monsters
	painchance "Stunner",120
	Health 1500
	Radius 24
	Height 64
	Mass 2000
	PainChance 15
	Monster
	+BOSS
	+FLOORCLIP
	+LOOKALLAROUND
	+MISSILEEVENMORE
	+NORADIUSDMG
	SeeSound "superbaron/scream"
	PainSound "superbaron/pain"
	DeathSound "superbaron/death"
	ActiveSound "superbaron/act"
	Obituary "%o was slaughtered by a Bruiserdemon."
	HitObituary "%o was cremated by a Bruiserdemon."
	Scale 1.25
	BloodColor "red"
	States
	{
	Spawn:
		BRUS AB 10 Bright A_Look
		Loop
	Melee:
		BRUS EF 6 Bright A_FaceTarget
		BRUS G 6 Bright A_CustomComboAttack("BruiserBall",32,random(1,8)*20,"baron/melee")
		Goto See
	Missile:
		BRUS E 0 A_Jump(144,8)
		BRUS EF 6 Bright A_FaceTarget
		BRUS G 6 Bright A_CustomMissile("BruiserBall")
		BRUS G 0 Bright A_Jump(96,1)
		Goto See
		BRUS HI 6 Bright A_FaceTarget
		BRUS J 6 Bright A_CustomMissile("BruiserBall")
		Goto See
		BRUS E 0 A_Jump(96,20)
		BRUS E 6 Bright A_FaceTarget
		BRUS F 4 Bright A_FaceTarget
		BRUS F 2 Bright A_Custommissile("BruiserBall2",56,0,-40,0)
		BRUS G 1 Bright A_Custommissile("BruiserBall2",56,0,-30,0)
		BRUS G 1 Bright A_Custommissile("BruiserBall2",56,0,-20,0)
		BRUS G 1 Bright A_Custommissile("BruiserBall2",56,0,-10,0)
		BRUS G 1 Bright A_Custommissile("BruiserBall2",56,0,0,0)
		BRUS G 1 Bright A_Custommissile("BruiserBall2",56,0,10,0)
		BRUS G 1 Bright A_Custommissile("BruiserBall2",56,0,20,0)
		BRUS G 0 Bright A_Jump(96,1)
		Goto See
		BRUS H 6 Bright A_FaceTarget
		BRUS I 4 Bright A_FaceTarget
		BRUS I 2 Bright A_Custommissile("BruiserBall2",56,0,40,0)
		BRUS J 1 Bright A_Custommissile("BruiserBall2",56,0,30,0)
		BRUS J 1 Bright A_Custommissile("BruiserBall2",56,0,20,0)
		BRUS J 1 Bright A_Custommissile("BruiserBall2",56,0,10,0)
		BRUS J 1 Bright A_Custommissile("BruiserBall2",56,0,0,0)
		BRUS J 1 Bright A_Custommissile("BruiserBall2",56,0,-10,0)
		BRUS J 1 Bright A_Custommissile("BruiserBall2",56,0,-20,0)
		Goto See
		BRUS KL 6 Bright A_FaceTarget
		BRUS M 2 Bright A_Custommissile("BruiserFireSpawner",16,0,-15,0)
		BRUS M 2 Bright A_Custommissile("BruiserFireSpawner",16,0,0,0)
		BRUS M 2 Bright A_Custommissile("BruiserFireSpawner",16,0,15,0)
		Goto See
	Pain:
		BRUS N 5 Bright A_Pain
		Goto See
	Pain.Stunner:
		BRUS N 2 A_ChangeFlag("NoPain",1)
		BRUS N 300 A_Pain
		BRUS N 0 A_ChangeFlag("NoPain",0)
		goto See
	Death:
		TNT1 A 0 A_GiveToTarget("AwardItem",24)
		BRUD A 6 Bright A_Scream
		BRUD BCDEFG 4 Bright
		BRUD H 4 Bright A_Fall
		BRUD IJKLMNOP 4 Bright
		BRUD QRSTUV 4
		BRUD W 3000
		BRUD W 2 A_FadeOut(0.1)
		Wait
	Raise:
		Stop
	}
}

ACTOR BruiserBall
{
	Radius 16
	Height 16
	Speed 18
	Damage 16
	Projectile
	+RANDOMIZE
	RenderStyle Add
	Alpha 0.9
	SeeSound "imp/attack"
	DeathSound "imp/shotx"
	States
	{
	Spawn:
		BRBA AABB 2 BRIGHT A_SpawnItemEx("BruiserBallTrail")
		Loop
	Death:
		BRBA KLMNOPQRSTUVWX 3 Bright
		Stop
	}
}

ACTOR BruiserBall2 : BruiserBall
{
	Radius 8
	Height 8
	Speed 10
	Damage 8
	States
	{
	Spawn:
		BRB2 AB 6 BRIGHT
		Loop
	Death:
		BRB2 CDEFGHI 3 Bright
		Stop
	}
}

ACTOR BruiserFireSpawner
{
	Radius 8
	Height 8
	Speed 12
	+RIPPER
	+FLOORHUGGER
	+BLOODLESSIMPACT
	Projectile
	States
	{
	Spawn:
		TNT1 A 3
		TNT1 A 6 A_CustomMissile("BruiserFire",0)
		Loop
	}
}

actor BruiserBallTrail
{
	+CLIENTSIDEONLY
	Radius 1
	Height 1
	PROJECTILE
	RENDERSTYLE ADD
	ALPHA 0.70
	+NOCLIP
	States
	{
	Spawn:
		BRBA CDEFGHIJ 4 Bright
		Stop
	}
}

ACTOR BruiserFire
{
	Radius 1
	Height 1
	Damage 20
	PROJECTILE
	RENDERSTYLE ADD
	ALPHA 0.9
	+NOCLIP
	+FLOORHUGGER
	SeeSound "weapons/rocklx"
	States
	{
	Spawn:
		XXBF AB 3 Bright
		XXBF C 3 Bright A_Explode(48,128,0)
		XXBF DEFGHIJKLMNOPQRST 3 Bright
		Stop
	}
}