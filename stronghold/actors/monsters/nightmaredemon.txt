ACTOR NightmareDemon : StrongholdDemon 10100 //NIGHTMARE DEMON
{
	//$Category Stronghold_Monsters
	painchance "Stunner",128
	Health 260
	Speed 8
	Mass 200
	PainChance 200
	RenderStyle TRANSLUCENT
	Alpha 0.6
	MeleeThreshold 196
	SeeSound "nightmareDemon/sight"
	AttackSound "nightmareDemon/melee"
	PainSound "nightmareDemon/pain"
	DeathSound "nightmareDemon/death"
	ActiveSound "nightmareDemon/active"
	Obituary "%o was spooked by a Nightmare Demon."
	States
	{
	Spawn:
		SAR2 AB 10 A_look
		Loop
	Melee:
		SAR2 EF 6 Fast A_FaceTarget
		SAR2 G 6 Fast A_SargAttack
		Goto See
	Missile:
		SAR2 O 6 Fast A_FaceTarget
		SAR2 P 8 Fast A_CustomMissile("NDemonBall",24)
		Goto See
	Death:
		TNT1 A 0 A_GiveToTarget("AwardItem",8)
		SAR2 I 5
		SAR2 J 5 A_Scream
		SAR2 K 5
		SAR2 L 5 A_Fall
		SAR2 M 5
		SAR2 N 3000
		SAR2 N 2 A_FadeOut(0.1)
		Wait
	}
}

ACTOR NDemonBall //NIGHTMARE DEMON - Fireball
{
	Radius 8
	Height 6
	Speed 12
	Damage 4
	PROJECTILE
	SeeSound "64ball/shoot"
	DeathSound "64ball/explode"
	States
	{
	Spawn:
		NBAL AB 4 Bright
		Loop
	Death:
		NBAL CDEFGH 4 Bright
		Stop
	}
}