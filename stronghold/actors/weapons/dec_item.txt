ACTOR StrongholdScriptedMarine : ScriptedMarine
{
	Speed 2
	States
	{
	See:
		PLAY A 1 A_Chase
		PLAY AAA 1 A_Chase("","",CHF_NOPLAYACTIVE)
		PLAY B 1 A_Chase
		PLAY BBB 1 A_Chase("","",CHF_NOPLAYACTIVE)
		PLAY C 1 A_Chase
		PLAY CCC 1 A_Chase("","",CHF_NOPLAYACTIVE)
		PLAY D 1 A_Chase
		PLAY DDD 1 A_Chase("","",CHF_NOPLAYACTIVE)
		Loop
	Melee.Fist:
		PLAY E 4 A_FaceTarget
		PLAY E 4 A_M_Punch(1)
		PLAY A 9
		PLAY A 0 A_M_Refire(1)
		Loop
		PLAY A 5 A_FaceTarget
		Goto See
	Melee.Berserk:
		PLAY E 4 A_FaceTarget
		PLAY E 4 A_M_Punch(10)
		PLAY A 9
		PLAY A 0 A_M_Refire(1)
		Loop
		PLAY A 5 A_FaceTarget
		Goto See
	Melee.Chainsaw:
		PLAY E 4 A_MarineNoise
		PLAY E 4 A_M_Saw
		PLAY E 0 A_M_SawRefire
		Goto Melee.Chainsaw+1
		PLAY A 0
		Goto See
	Missile.Pistol:
		PLAY E 4 A_FaceTarget
		PLAY F 6 Bright A_M_FirePistol(1)
		PLAY A 4 A_FaceTarget
		PLAY A 0 A_M_Refire
		PLAY A 5
		Goto See
	Fireloop.Pistol:
		PLAY F 6 Bright A_M_FirePistol(0)
		PLAY A 4 A_FaceTarget
		PLAY A 0 A_M_Refire
		Goto Fireloop.Pistol
		PLAY A 5
		Goto See
	Missile.Shotgun:
		PLAY E 3 A_M_CheckAttack
		PLAY F 7 Bright A_M_FireShotgun
		Goto See
	Missile.SSG:
		PLAY E 3 A_M_CheckAttack
		PLAY F 7 Bright A_M_FireShotgun2
		Goto See
	Missile.Chaingun:
		PLAY E 4 A_FaceTarget
		PLAY FF 4 Bright A_M_FireCGun(1)
		PLAY FF 4 Bright A_M_FireCGun(0)
		PLAY A 0 A_M_Refire
		Goto Missile.Chaingun+3
		PLAY A 0
		Goto See
	Missile.Rocket:
		PLAY E 8
		PLAY F 6 Bright A_M_FireMissile
		PLAY E 6
		PLAY A 0 A_M_Refire
		Loop
		PLAY A 0
		Goto See
	Missile.Plasma:
		PLAY E 2 A_FaceTarget
		PLAY E 0 A_FaceTarget
		PLAY F 3 Bright A_M_FirePlasma
		PLAY A 0 A_M_Refire
		Goto Missile.Plasma+1
		PLAY A 0
		Goto See
	Missile.Railgun:
		PLAY E 4 A_M_CheckAttack
		PLAY F 6 Bright A_M_FireRailgun
		Goto See
	Missile.BFG:
		PLAY E 5 A_M_BFGSound
		PLAY EEEEE 5 A_FaceTarget
		PLAY F 6 Bright A_M_FireBFG
		PLAY A 4 A_FaceTarget
		PLAY A 0 A_M_Refire
		Loop
		PLAY A 0
		Goto See
	SkipAttack:
		PLAY A 1
		Goto See
	Raise:
		PLAY MLKJIH 5
		Goto See
	}
}

ACTOR StrongholdMarineFist : StrongholdScriptedMarine replaces MarineFist
{
	States
	{
	Melee:
		Goto Super::Melee.Fist
	Missile:
		Stop
	}
}

ACTOR StrongholdMarineBerserk : StrongholdScriptedMarine replaces MarineBerserk
{
	States
	{
	Melee:
		Goto Super::Melee.Berserk
	Missile:
		Stop
	}
}

ACTOR StrongholdMarineChainsaw : StrongholdScriptedMarine replaces MarineChainsaw
{
	States
	{
	Melee:
		Goto Super::Melee.Chainsaw
	Missile:
		Stop
	}
}

ACTOR StrongholdMarinePistol : StrongholdScriptedMarine replaces MarinePistol
{
	States
	{
	Missile:
		Goto Super::Missile.Pistol
	}
}

ACTOR StrongholdMarineShotgun : StrongholdScriptedMarine replaces MarineShotgun
{
	States
	{
	Missile:
		Goto Super::Missile.Shotgun
	}
}

ACTOR StrongholdMarineSSG : StrongholdScriptedMarine replaces MarineSSG
{
	States
	{
	Missile:
		Goto Super::Missile.SSG
	}
}

ACTOR StrongholdMarineChaingun : StrongholdScriptedMarine replaces MarineChaingun
{
	States
	{
	Missile:
		Goto Super::Missile.Chaingun
	}
}

ACTOR StrongholdMarineRocket : StrongholdMarineFist replaces MarineRocket
{
	States
	{
	Missile:
		Goto Super::Missile.Rocket
	}
}

ACTOR StrongholdMarinePlasma : StrongholdScriptedMarine replaces MarinePlasma
{
	States
	{
	Missile:
		Goto Super::Missile.Plasma
	}
}

ACTOR StrongholdMarineRailgun : StrongholdScriptedMarine replaces MarineRailgun
{
	States
	{
	Missile:
		Goto Super::Missile.Railgun
	}
}

ACTOR StrongholdMarineBFG : StrongholdScriptedMarine replaces MarineBFG
{
	States
	{
	Missile:
		Goto Super::Missile.BFG
	}
}

/////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
// MARINE REINFORCEMENTS ////////////////////////////////////////////////////////////////////////////////////////////////////////////////
/////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////

actor MarineItem : CustomInventory 25003
{
	//$Category Stronghold_Items
	inventory.pickupmessage "Picked up some marine reinforcements."
	inventory.maxamount 5
	Tag "Marine Reinforcements"
	inventory.icon MARIA0
	+INVENTORY.INVBAR
	+GHOST
	scale 0.75
	states
	{
	Spawn:
		MARP A -1 BRIGHT
		stop
	Use:
		TNT1 A 1 A_SpawnItemEx("TeleportFog",64,0,16,0,0,0,0,SXF_TRANSFERTRANSLATION)
		TNT1 A 0 A_Jump(128,4)
		TNT1 A 0 A_Jump(128,2)
		TNT1 A 0 A_SpawnItemEx("STMarineShotgun",64,0,16,0,0,0,0,SXF_TRANSFERTRANSLATION)
		TNT1 A 0 A_SpawnItemEx("STMarineChaingun",64,0,16,0,0,0,0,SXF_TRANSFERTRANSLATION)
		TNT1 A 0 A_Jump(128,2)
		TNT1 A 0 A_SpawnItemEx("STMarineSSG",64,0,16,0,0,0,0,SXF_TRANSFERTRANSLATION)
		TNT1 A 0 A_Jump(128,2)
		TNT1 A 0 A_SpawnItemEx("STMarineRocket",64,0,16,0,0,0,0,SXF_TRANSFERTRANSLATION)
		TNT1 A 0 A_SpawnItemEx("STMarinePlasma",64,0,16,0,0,0,0,SXF_TRANSFERTRANSLATION)
		stop
	}
}

ACTOR STScriptedMarine : StrongholdScriptedMarine
{
	SeeSound "marine/sight"
	ActiveSound "marine/active"
	DeathSound "marine/death"
	PainSound "marine/pain"
	MeleeSound "marine/punch"
	health 300
	-ACTIVATEMCROSS
	+NORADIUSDMG
	+GHOST
	+NOTIMEFREEZE
	States
	{
	Raise:
		stop
	}
}

actor STMarineShotgun : STScriptedMarine 12851
{
	//$Category Stronghold_Marines
	Obituary "%o was shot down like a clay pigeon."
	States
	{
	Missile:
		Goto Super::Missile.Shotgun
	}
}

actor STMarineChaingun : STScriptedMarine 12852
{
	//$Category Stronghold_Marines
	Obituary "%o was ventilated by a chaingun-toting marine."
	States
	{
	Missile:
		Goto Super::Missile.Chaingun
	}
}

actor STMarineSSG : STScriptedMarine 12853
{
	//$Category Stronghold_Marines
	Obituary "%o downed a devastating dosage of double-barrelled death."
	States
	{
	Missile:
		Goto Super::Missile.SSG
	}
}

actor STMarineRocket : STScriptedMarine 12854
{
	//$Category Stronghold_Marines
	Obituary "%o boarded the express train to oblivion on a marine's rocket."
	health 600
	States
	{
	Missile:
		PLAY E 8
		PLAY F 6 Bright A_M_FireMissile
		PLAY A 0 A_Jump(96,"See")
		Loop
	}
}

actor STMarinePlasma : STScriptedMarine 12855
{
	//$Category Stronghold_Marines
	Obituary "%o was atomized by a steady stream of blazing plasma."
	health 200
	States
	{
	Missile:
		Goto Super::Missile.Plasma
	}
}

//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
// SENTRY ////////////////////////////////////////////////////////////////////////////////////////////////////////////////
//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////

actor Sentry 12856
{
	//$Category Stronghold_Marines
	mass 9999999999
	damagefactor "overmindfactor", 10
	radius 16
	health 300
	maxstepheight 512
	maxdropoffheight 512
	+shootable
	+noblood
	+friendly
	+lookallaround
	+missilemore
	+missileevenmore
	+NORADIUSDMG
	states
 	{
 	spawn:
 		DGUN A 4 a_look
 		loop
 	see:
 		DGUN A 4 A_chase
 		loop
 	missile:
 		DGUN A 4 A_facetarget
 		DGUN B 4 A_custommissile("SentryPlasma",16,-10)
 		DGUN C 4 A_custommissile("SentryPlasma",16,10)
 		DGUN A 7 A_CPosRefire //was A 4
 		loop
 	death:
 		TNT1 A 1
 		stop
 	}
}

actor SentryPlasma : PlasmaBall
{
	-RANDOMIZE
	RenderStyle Normal
	Alpha 1
	radius 1
	height 2
	speed 20
	damage 3
	scale 0.3
	damagetype "bossfactor"
	+DONTBLAST
	+THRUGHOST
	translation "192:207=208:223","240:246=223:223"
	SeeSound "weapons/repfir"
	DeathSound "weapons/railf1"
	states
 	{
 	spawn:
   		PLSS AB 4 Bright
   		loop
 	}
}

actor SentryItem : CustomInventory 25004
{
	//$Category Stronghold_Items
	inventory.pickupmessage "Picked up a deployable sentry turret."
	inventory.maxamount 3
	inventory.icon SENIA0
	Tag "Sentry Turret"
	+INVENTORY.INVBAR
	scale 0.75
	states
	{
	Spawn:
		SENP A -1 BRIGHT
		stop
	use:
  		TNT1 A 1 A_PlaySound("DSSTPLAC",CHAN_AUTO,0.7)
		TNT1 A 1 A_SpawnItemEx("ItemFog",64,0,16,0,0,0,0,SXF_TRANSFERTRANSLATION)
		TNT1 A 1 A_SpawnItemEx("Sentry",64,0,0,0,0,0,0,SXF_TRANSFERTRANSLATION)
  		stop
	}
}

//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
// BARREL SPAWNER ////////////////////////////////////////////////////////////////////////////////////////////////////////////////
//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////

actor BarrelItem : CustomInventory 25005
{
	//$Category Stronghold_Items
	inventory.pickupmessage "Picked up a portable barrel."
	inventory.maxamount 10
	inventory.icon BARIA0
	Tag "Barrel"
	+INVENTORY.INVBAR
	scale 0.75
	states
	{
	Spawn:
		BARP A -1 BRIGHT
		stop
	Use:
		TNT1 A 1 A_SpawnItemEx("ItemFog",64,0,16,0,0,0,0,SXF_TRANSFERTRANSLATION)
		TNT1 A 0 A_SpawnItemEx("ExplosiveBarrelTimer",64,0,16,0,0,0,0,SXF_TRANSFERTRANSLATION)
		stop
	}
}

actor ExplosiveBarrelTimer
{
	obituary "%o went boom."
	health 20
	radius 10
	height 34
	deathsound "world/barrelx"
	MONSTER
	+SOLID
	+SHOOTABLE
	+NOBLOOD
	+DONTGIB
	+NOICEDEATH
	+FRIENDLY
	+NORADIUSDMG
	+INVULNERABLE
	states
	{
	Spawn:
		BAR1 A 35
		BAR1 A 0 A_ChangeFlag("INVULNERABLE",0)
		BAR1 AB 6 A_Look
		Goto Spawn+2
	See:
		BAR1 AB 2 A_Chase
		loop
	Melee:
		BAR1 ABABABABABABABABABABABABABABABABAB 6
		BAR1 A 0 A_Die
	Death:
		BEXP A 5 bright
		BEXP B 5 bright A_Scream
		BEXP C 5 bright
		BEXP D 10 bright A_Explode
		BEXP E 10 bright
		stop
	}
}

/////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
// COINS ////////////////////////////////////////////////////////////////////////////////////////////////////////////////
/////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////

actor CoinItem : CustomInventory 25006
{
	//$Category Stronghold_Items
	ConversationID 300
	gravity 0.6
	inventory.pickupmessage "Picked up a coin."
	inventory.maxamount 100000
	inventory.icon SACKA0
	Tag "Credits"
	+INVENTORY.KEEPDEPLETED
	states
	{
	Spawn:
		COIN A 1500
		COIN A 2 A_FadeOut
		Wait
	Use:
		//TODO: Make it so it drops 10 or so coins
		TNT1 A 0 A_SpawnItemEx("CoinItem",64,0,16)
		stop
	}
}

// Item that monsters give player when they die. An ACS script gives the player the equal amount in CoinItem.
actor AwardItem : Inventory
{
	+INVENTORY.UNDROPPABLE
	inventory.maxamount 1000000
}

/////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
// LIFES ////////////////////////////////////////////////////////////////////////////////////////////////////////////////
/////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////

actor LifeItem : CustomInventory 25926
{
	//$Category Stronghold_Items
	scale 0.75
	inventory.pickupmessage "Picked up an extra life!"
	inventory.maxamount 9
	inventory.icon HERIA0
	Tag "Lives (Multiplayer Use Only)"
	+INVENTORY.INVBAR
	+INVENTORY.KEEPDEPLETED
	+INVENTORY.UNDROPPABLE
	states
	{
	Spawn:
		HERP A -1 Bright
		stop
	Use:
		TNT1 A 0 ACS_ExecuteWithResult(897) // Allows the script to fail
		stop
	}
}

/////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
// JAILTRACKER //////////////////////////////////////////////////////////////////////////////////////////////////////////
/////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////

// Player is given this when they're in jail
actor JailTracker : Inventory
{
	+INVENTORY.UNDROPPABLE
	+INVENTORY.INTERHUBSTRIP
}

// Player is given one of these per each player in jail
actor JailTrackerAllPlayers : Inventory
{
	+INVENTORY.UNDROPPABLE
	+INVENTORY.INTERHUBSTRIP
	inventory.maxamount 10000
}

/////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
// THRUSTDUMMY //////////////////////////////////////////////////////////////////////////////////////////////////////////
/////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////

// Used for scripts that thrust player to detect when he's not where he should be
actor ThrustDummy : Inventory
{
	+INVENTORY.UNDROPPABLE
	+INVENTORY.INTERHUBSTRIP
}

////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
// SUPER SHIELD ////////////////////////////////////////////////////////////////////////////////////////////////////////////////
////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////

actor SuperShield : BasicArmorPickup 10716
{
	//$Category Stronghold_Items
	armor.savepercent 100
	armor.saveamount 300
	inventory.icon "ARMXA0"
	inventory.pickupmessage "Picked up the super shield!"
	states
	{
	Spawn:
		ARMX AB 3 Bright
		loop
	}
}

/////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
// PORTABLE MEDICINE ////////////////////////////////////////////////////////////////////////////////////////////////////////////////
/////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////

actor PMedKit : HealthPickup 12792
{
	//$Category Stronghold_Items
	health 25
	inventory.maxamount 10
	inventory.icon "PMH1A0"
	inventory.pickupmessage "Picked up a portable medkit."
	Tag "Portable Medkit"
	+COUNTITEM
	states
	{
	Spawn:
		PM01 AABC 2
		loop
	}
}

actor PSuperMedKit : PMedKit 12791
{
	//$Category Stronghold_Items
	health 100
	inventory.icon "PMH2A0"
	inventory.pickupmessage "Picked up a portable super medkit."
	Tag "Portable Super Medkit"
	states
	{
	Spawn:
		PM02 A -1
		stop
	}
}

/////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
// POWER UPS ////////////////////////////////////////////////////////////////////////////////////////////////////////////////
/////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////

//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
// TIME FREEZER //////////////////////////////////////////////////////////////////////////////////////////////////////////////
//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
actor TimeFreezerItem : PowerupGiver 12793
{
	//$Category Stronghold_Items
	scale 0.75
	inventory.pickupmessage "Picked up a time freeze sphere."
	inventory.icon "FRZIA0"
	inventory.maxamount 3
	inventory.usesound "pickups/slowmo"
	powerup.color "00 33 FF" 0.10
	powerup.type "TimeFreezer"
	powerup.duration 1050
	Tag "Time Freeze"
	+INVENTORY.FANCYPICKUPSOUND
	+INVENTORY.INVBAR
	states
	{
	Spawn:
		FRZP A -1 BRIGHT
		stop
	}
}

actor TimeFreezerPickup : TimeFreezerItem 24027
{
	//$Category Stronghold_Items
	inventory.pickupmessage "Time freeze!"
	inventory.maxamount 10
	inventory.icon "TIMEZ0"
	states
	{
	Spawn:
		TIME ABCD 4 BRIGHT LIGHT("TimeSphere")
		loop
	}
}

///////////////////////////////////////////////////////////////////////////////////////////////////////////////
// QUAD DAMAGE ////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////////

actor PowerQuadDamage : PowerDamage
{
	damagefactor "normal", 4
	inventory.icon "DOOMA0"
}

actor QuadDamageItem : PowerupGiver 12794
{
	//$Category Stronghold_Items
	scale 0.75
	inventory.pickupmessage "Picked up a quad damage sphere."
	powerup.color RedMap
	inventory.maxamount 3
	inventory.usesound "pickups/slowmo"
	inventory.icon "DAMIA0"
	powerup.type QuadDamage
	powerup.duration 1050
	Tag "Quad Damage"
	+INVENTORY.FANCYPICKUPSOUND
	states
	{
	Spawn:
		DAMP A -1 BRIGHT
		stop
	}
}

actor QuadDamagePowerup : QuadDamageItem 24028
{
	//$Category Stronghold_Items
	inventory.pickupmessage "Doomsphere!"
	inventory.icon "DOOMA0"
	+INVENTORY.AUTOACTIVATE
	states
	{
	Spawn:
		DOOM ABCB 4 BRIGHT LIGHT("DoomSphere")
		loop
	}
}

//////////////////////////////////////////////////////////////////////////////////////////////////////////////
// INVULNERABILITY ///////////////////////////////////////////////////////////////////////////////////////////
//////////////////////////////////////////////////////////////////////////////////////////////////////////////

actor InvulnerabilityItem : PowerupGiver 12795
{
	//$Category Stronghold_Items
	scale 0.75
	inventory.pickupmessage "Picked up an invulnerability sphere."
	inventory.icon "INVIA0"
	inventory.maxamount 3
	inventory.usesound "pickups/slowmo"
	powerup.color InverseMap
	powerup.type "Invulnerable"
	powerup.duration 1575
	Tag "Invulnerability"
	+INVENTORY.FANCYPICKUPSOUND
	states
	{
	Spawn:
		INVP A -1 BRIGHT
		stop
	}
}

////////////////////////////////////////////////////////////////////////////////////////////////////////////////
// MAX ARMOR+ //////////////////////////////////////////////////////////////////////////////////////////////////
////////////////////////////////////////////////////////////////////////////////////////////////////////////////

ACTOR MaxArmorBonus2 : BasicArmorBonus 12796
{
	//$Category Stronghold_Items
	Inventory.Pickupmessage "Picked up a max armor bonus."
	Armor.Savepercent 33.33333
	Armor.Saveamount 5
	Armor.Maxsaveamount 200
	Armor.MaxBonus 5
	Armor.MaxBonusMax 50
	+COUNTITEM
	+INVENTORY.ALWAYSPICKUP
	states
	{
	Spawn:
		BON4 ABCDCB 6 BRIGHT
		loop
	}
}

////////////////////////////////////////////////////////////////////////////////////////////////////////////////
// MAX HEALTH+ /////////////////////////////////////////////////////////////////////////////////////////////////
////////////////////////////////////////////////////////////////////////////////////////////////////////////////

ACTOR MaxHealthBonus2 : UpgradeStamina 12797
{
	//$Category Stronghold_Items
	Inventory.Pickupmessage "Picked up a max health bonus."
	Inventory.Amount 5
	Inventory.MaxAmount 300
	+COUNTITEM
	+INVENTORY.ALWAYSPICKUP
	states
	{
	Spawn:
		BON3 ABCDCB 6 BRIGHT
		loop
	}
}

//////////////////////////////////////////////////////////////////////////////////////////////////////////////////
// REGENERATION //////////////////////////////////////////////////////////////////////////////////////////////////
//////////////////////////////////////////////////////////////////////////////////////////////////////////////////

actor RegenerationItem : PowerupGiver 24031
{
	//$Category Stronghold_Items
	scale 0.75
	inventory.pickupmessage "Picked up a regeneration sphere."
	inventory.icon "RESIA0"
	inventory.maxamount 3
	inventory.usesound "pickups/slowmo"
	powerup.color "FF 00 AE" 0.10
	powerup.type "Regeneration"
	powerup.duration 1575
	Tag "Regeneration"
	+INVENTORY.FANCYPICKUPSOUND
	states
	{
	Spawn:
		RESP A -1 BRIGHT
		stop
	}
}

///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
// BARRIER ////////////////////////////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////

actor WallItem : CustomInventory 12798
{
	//$Category Stronghold_Items
	inventory.pickupmessage "Picked up a barrier kit."
	inventory.pickupsound "misc/p_pkup"
	inventory.maxamount 3
	inventory.icon WALIA0
	Tag "Barrier Kit"
	+INVENTORY.INVBAR
	scale 0.75
	states
	{
	Spawn:
		WALP A -1 BRIGHT
		stop
	use:
  		TNT1 A 0 A_SpawnItemEx("WallSpawner",64,0,0,0,0,0,0,SXF_NOCHECKPOSITION)
		stop
	}
}

actor WallSpawner
{
	+ISMONSTER
	+NOBLOCKMAP
	Radius 4
	Height 4
	States
	{
	Spawn:
		TNT1 A 0
		TNT1 A 1 A_SpawnItemEx("WallPlaced",60, 32, 0, 0, 0, 0, 90, SXF_SETMASTER | SXF_NOCHECKPOSITION)
		TNT1 A 1 A_SpawnItemEx("WallPlaced",50, 32, 0, 0, 0, 0, 90, SXF_SETMASTER | SXF_NOCHECKPOSITION)
  		TNT1 A 1 A_SpawnItemEx("WallPlaced",40, 32, 0, 0, 0, 0, 90, SXF_SETMASTER | SXF_NOCHECKPOSITION)
		TNT1 A 1 A_SpawnItemEx("WallPlaced",30, 32, 0, 0, 0, 0, 90, SXF_SETMASTER | SXF_NOCHECKPOSITION)
  		TNT1 A 1 A_SpawnItemEx("WallPlaced",20, 32, 0, 0, 0, 0, 90, SXF_SETMASTER | SXF_NOCHECKPOSITION)
  		TNT1 A 1 A_SpawnItemEx("WallPlaced",10, 32, 0, 0, 0, 0, 90, SXF_SETMASTER | SXF_NOCHECKPOSITION)
  		TNT1 A 1 A_SpawnItemEx("WallPlaced",0,  32, 0, 0, 0, 0, 90,  SXF_SETMASTER | SXF_NOCHECKPOSITION)
  		TNT1 A 1 A_SpawnItemEx("WallPlaced",-10,32, 0, 0, 0, 0, 90, SXF_SETMASTER | SXF_NOCHECKPOSITION)
		TNT1 A 1 A_SpawnItemEx("WallPlaced",-20,32, 0, 0, 0, 0, 90, SXF_SETMASTER | SXF_NOCHECKPOSITION)
  		TNT1 A 1 A_SpawnItemEx("WallPlaced",-30,32, 0, 0, 0, 0, 90, SXF_SETMASTER | SXF_NOCHECKPOSITION)
  		TNT1 A 1 A_SpawnItemEx("WallPlaced",-40,32, 0, 0, 0, 0, 90, SXF_SETMASTER | SXF_NOCHECKPOSITION)
		TNT1 A 1 A_SpawnItemEx("WallPlaced",-50,32, 0, 0, 0, 0, 90, SXF_SETMASTER | SXF_NOCHECKPOSITION)
		TNT1 A 1 A_SpawnItemEx("WallPlaced",-60,32, 0, 0, 0, 0, 90, SXF_SETMASTER | SXF_NOCHECKPOSITION)
		Stop
	}
}

actor WallBolt
{
	PROJECTILE
	+NOCLIP
	Speed 1
	RenderStyle Add
	Scale 0.33
	States
	{
	Spawn:
		WLXP A 1 A_FadeOut(0.025)
		Loop
	}
}

actor WallBolt2 : WallBolt
{
	States
	{
	Spawn:
		WLXP B 1 A_FadeOut(0.025)
		Loop
	}
}

actor WallPlaced
{
	health 200
	mass 0x7fffffff
	radius 8
	height 128
	scale 0.5
	+FRIENDLY
	+DONTBLAST
	+SOLID
	+SHOOTABLE
	+NOBLOOD
	+NORADIUSDMG
	+ISMONSTER
	DeathSound "WallItem/Explode"
	var int user_timer;
	states
 	{
	Spawn:
		TNT1 A 0
		TNT1 A 0 A_JumpIf(user_timer>=4200,3)
		TNT1 A 0 A_SpawnItemEx("RedSparkleRandom",random(-10.0,10.0),0,0,0,0,random(0.05,2.00),0,0,192)
		TNT1 A 1 A_SetUserVar("user_timer",user_timer+1)
  		Loop
		TNT1 A 1 A_Die
 	Death:
 		TNT1 A 0 A_Scream
		TNT1 A 0 A_KillSiblings
		TNT1 AAAA 0 A_CustomMissile("WallBolt",random(10,50),0,random(0,360),2,random(-15,15))
		TNT1 AAAA 0 A_CustomMissile("WallBolt2",random(10,50),0,random(0,360),2,random(-15,15))
 		Stop
  	}
}

actor RedSparkle
{
	+CLIENTSIDEONLY
	height 2
	radius 1
	+MISSILE
	+NOINTERACTION
	renderstyle Add
	alpha 0.9
	translation "80:96=175:191"
	states
	{
	Spawn:
		PTCL ABCABCABCABCABCABC 2 bright
	Death:
		PTCL ABC 2 bright A_FadeOut
		loop
	}
}

actor RedSparkleRandom : RandomSpawner
{
	DropItem "RedSparkleTiny"
	DropItem "RedSparkleSmall"
	DropItem "RedSparkleMedium"
}

actor RedSparkleTiny   : RedSparkle { scale 0.125 }
actor RedSparkleSmall  : RedSparkle { scale 0.2 }
actor RedSparkleMedium : RedSparkle { scale 0.4 }

/////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
// RANDOM WEAPON ////////////////////////////////////////////////////////////////////////////////////////////////////////////////
/////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////

actor RandomWeaponItem : CustomInventory 25023
{
	//$Category Stronghold_Items
	inventory.pickupmessage "Picked up a random weapon sphere."
	inventory.maxamount 5
	inventory.icon WEPIA0
	Tag "Random Weapon"
	+INVENTORY.INVBAR
	scale 0.75
	states
	{
	Spawn:
		WEPP A -1 BRIGHT
		stop
	Use:
		TNT1 A 0 A_Jump(64, 21)
		TNT1 A 0 A_Jump(64, 19)
		TNT1 A 0 A_Jump(64, 17)
		TNT1 A 0 A_Jump(64, 15)
		TNT1 A 0 A_Jump(64, 13)
		TNT1 A 0 A_Jump(64, 11)
		TNT1 A 0 A_Jump(64, 9)
		TNT1 A 0 A_Jump(64, 7)
		TNT1 A 0 A_Jump(64, 5)
		TNT1 A 0 A_Jump(64, 3)
		TNT1 A 0 A_Jump(64, 1)
		TNT1 A 0 A_GiveInventory("StrongholdBFG9000", 4)
		stop
		TNT1 A 0 A_GiveInventory("Pyrocannon", 4)
		stop
		TNT1 A 0 A_GiveInventory("Devastator", 4)
		stop
		TNT1 A 0 A_GiveInventory("Repeater", 4)
		stop
		TNT1 A 0 A_GiveInventory("Flamer", 4)
		stop
		TNT1 A 0 A_GiveInventory("Railgun", 4)
		stop
		TNT1 A 0 A_GiveInventory("StunnerRifle", 4)
		stop
		TNT1 A 0 A_GiveInventory("StrongholdPlasmaRifle", 4)
		stop
		TNT1 A 0 A_GiveInventory("StrongholdRLauncher", 4)
		stop
		TNT1 A 0 A_GiveInventory("HRL", 4)
		stop
		TNT1 A 0 A_GiveInventory("LandMineLayer", 4)
		stop
		TNT1 A 0 A_GiveInventory("Autoshotgun", 4)
		stop
	}
}

//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
// AUTO DRONE ////////////////////////////////////////////////////////////////////////////////////////////////////////////////
//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////

ACTOR AutoDroneItem : CustomInventory 24025
{
	//$Category Stronghold_Items
	inventory.pickupmessage "Picked up an autodrone."
	inventory.maxamount 2
	inventory.icon DRNIA0
	Tag "Autodrone"
	+INVENTORY.INVBAR
	scale 0.75
	states
	{
	Spawn:
		DRNP A -1 BRIGHT
		stop
	Use:
		TNT1 A 0 A_JumpIfInventory("ForceCubeTimerRight",1,3)
		TNT1 A 0 A_GiveInventory("ForceCubeTimerRight",1)
		TNT1 A 0 A_SpawnItemEx("ForceCube1Right",0,0,0,0,0,0,0,33)
		Stop
		TNT1 A 0 A_JumpIfInventory("ForceCubeTimerLeft",1,3)
		TNT1 A 0 A_GiveInventory("ForceCubeTimerLeft",1)
		TNT1 A 0 A_SpawnItemEx("ForceCube1Left",0,0,0,0,0,0,0,33)
		Stop
		TNT1 A 0
		Loop
	}
}

ACTOR ForceCube1Right
{
	Radius 0
	Height 0
	+NOTIMEFREEZE
	States
	{
	Spawn:
		TNT1 A 0
		TNT1 A 0 A_SpawnItemEx("ForceCubeGiverRight",0,0,0,0,0,0,0,33)
		Stop
	}
}

ACTOR ForceCube1Left : ForceCube1Right
{
	States
	{
	Spawn:
		TNT1 A 0
		TNT1 A 0 A_SpawnItemEx("ForceCubeGiverLeft",0,0,0,0,0,0,0,33)
		Stop
	}
}

ACTOR ForceCube2Right : CustomInventory
{
	+AUTOACTIVATE
	+NOTIMEFREEZE
	States
	{
	Pickup:
		TNT1 A 0 A_SpawnItemEx("RightCube",28,16,36,0,0,0,0,33)
		Stop
	}
}

ACTOR ForceCube2Left : CustomInventory
{
	States
	{
	Pickup:
		TNT1 A 0 A_SpawnItemEx("LeftCube",28,-16,36,0,0,0,0,33)
		Stop
	}
}

ACTOR ForceCubeTimerLeft : CustomInventory
{
	+NOTIMEFREEZE
	+INVENTORY.INTERHUBSTRIP
	States
	{
	Use:
		TNT1 A 0
		Stop
	}
}

ACTOR ForceCubeTimerRight : ForceCubeTimerLeft{}

ACTOR ForceCubeTimer2 : Inventory
{
	+NOTIMEFREEZE
	Inventory.MaxAmount 4200
}

ACTOR ForceCubeGiverRight
{
	Health 1000000000
	Radius 0
	Height 0
	Speed 0
	+NONSHOOTABLE
	+MISSILEMORE
	+MISSILEEVENMORE
	+NOTIMEFREEZE
	MinMissileChance 0
	States
	{
	Spawn:
		TNT1 A 1 A_Look
		Loop
	See:
		TNT1 A 1 A_Chase
		Loop
	Missile:
		TNT1 A 0 A_JumpIfInTargetInventory("ForceCubeTimerRight",1,2)
		TNT1 A 0 A_Jump(256,"Death")
		TNT1 A 0 A_JumpIfInventory("ForceCubeTimer2",4200,"Death")
		TNT1 A 0 A_GiveInventory("ForceCubeTimer2",1)
		TNT1 A 1 A_GiveToTarget("ForceCube2Right",1)
		Loop
	Death:
		TNT1 A 0 A_TakeFromTarget("ForceCubeTimerRight",1)
		Stop
	}
}

ACTOR ForceCubeGiverLeft : ForceCubeGiverRight
{
	States
	{
	Missile:
		TNT1 A 0 A_JumpIfInTargetInventory("ForceCubeTimerLeft",1,2)
		TNT1 A 0 A_Jump(256,"Death")
		TNT1 A 0 A_JumpIfInventory("ForceCubeTimer2",4200,"Death")
		TNT1 A 0 A_GiveInventory("ForceCubeTimer2",1)
		TNT1 A 1 A_GiveToTarget("ForceCube2Left",1)
		Loop
	Death:
		TNT1 A 0 A_TakeFromTarget("ForceCubeTimerLeft",1)
		Stop
	}
}

ACTOR RightCube
{
	Radius 0
	Height 0
	Speed 0
	Scale 0.1
	+FRIENDLY
	+NOGRAVITY
	+NOTIMEFREEZE
	+MISSILEMORE
	+MISSILEEVENMORE
	MinMissileChance 0
	States
	{
	Spawn:
		DRON AA 1 A_LookEx(2,0,800,0,135,"See")
		Stop
	See:
		DRON A 0 A_Jump(240,3) // the higher the number, the less frequent it attacks
		DRON A 1 A_CustomMissile("AutoDroneBall",8,0,0,0)
		TNT1 A 0 A_PlaySound("ForceCube/Fire")
		DRON A 1
		Stop
	}
}

ACTOR LeftCube : RightCube
{
	States
	{
	See:
		DRON A 0 A_Jump(240,3) // the higher the number, the less frequent it attacks
		DRON A 1 A_CustomMissile("AutoDroneBall",8,0,0,0)
		TNT1 A 0 A_PlaySound("ForceCube/Fire")
		DRON A 1
		Stop
	}
}

ACTOR AutoDroneBall
{
	Radius 8
	Height 8
	Speed 20
	Damage 48
	PROJECTILE
	RENDERSTYLE ADD
	ALPHA 0.90
	Scale 0.2
	damagetype "bossfactor"
	+NOTIMEFREEZE
	+THRUGHOST
	SeeSound "weapons/devbal"
	DeathSound "weapons/devex2"
	States
	{
	Spawn:
		DBA2 AB 4 Bright
		Loop
	Death:
		DBA2 CDE 4 Bright
		Stop
	}
}

/////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
// SMARTBOMB ////////////////////////////////////////////////////////////////////////////////////////////////////////////////
/////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////

actor SmartBombItem : CustomInventory 24026
{
	//$Category Stronghold_Items
	inventory.pickupmessage "Picked up the smart bomb!"
	inventory.icon SMAIA0
	Tag "Smart Bomb"
	+INVENTORY.INVBAR
	scale 0.75
	states
	{
	Spawn:
		SMAP A -1 BRIGHT
		stop
	Use:
		TNT1 A 1 A_SpawnItemEx("SmartBombGrenade", 0, 0, 32, 0, 10, 6, 90)
		stop
	}
}

actor SmartBombGrenade
{
	projectile
	-nogravity
	+hexenbounce
	+bounceonactors
	+canbouncewater
	+noexplodefloor
	+slidesonwalls
	-noteleport
	bouncefactor 0.6
	damagetype "corefriendly"
	obituary "%o got blinded by %k."
	speed 6
	radius 2
	height 2
	States
	{
	Spawn:
		UVGR ABCD 1
		UVGR ABCD 2
		UVGR ABCD 4
		UVGR A 16 A_Stop
		UVGR A 0 A_PlaySound("UVCharge",CHAN_VOICE,1,0,ATTN_NONE)
		UVGR A 0 A_SpawnItemEx("smallestlightemmit")
		UVGR A 0 A_SpawnItemEx("smallerlightemmit")
		UVGR E 170 BRIGHT A_SpawnItemEx("smalllightemmit")
		UVGR E 0 A_SpawnItemEx("Blindinglightemmit")
		UVGR E 0 A_SpawnItemEx("MoreBlindinglightemmit")
		UVGR E 0 A_SpawnItemEx("MostBlindinglightemmit")
		UVGR E 0 A_CustomMissile("LightRailer", 0, 0, 0)
		UVGR E 0 Radius_Quake(9,17,0,64,0)
		UVGR E 1 BRIGHT A_SpawnItemEx("RingLightemmit1")
		UVGR E 1 BRIGHT A_SpawnItemEx("RingLightemmit2")
		UVGR E 1 BRIGHT A_SpawnItemEx("RingLightemmit3")
		UVGR E 1 BRIGHT A_SpawnItemEx("RingLightemmit4")
		UVGR E 1 BRIGHT A_SpawnItemEx("RingLightemmit5")
		TNT1 A 15
		stop
	}
}

actor smalllightemmit
{
	+CLIENTSIDEONLY
	radius 0
	height 0
	Renderstyle ADD
	alpha 0.75
	scale 0.5
	States
	{
	Spawn:
		WHFL A 170 BRIGHT
		WHFL A 1 BRIGHT A_Fadeout(0.05)
		Wait
	}
}

actor smallerlightemmit : smalllightemmit
{
	scale 0.35
}

actor smallestlightemmit : smalllightemmit
{
	scale 0.2
}

actor Blindinglightemmit : smalllightemmit
{
	alpha 0.5
	scale 1.0
	States
	{
	Spawn:
		WHFL A 50 BRIGHT
		WHFL A 1 BRIGHT A_Fadeout(0.05)
		Wait
	}
}

actor moreBlindinglightemmit : Blindinglightemmit
{
	scale 0.75
}

actor mostBlindinglightemmit : Blindinglightemmit
{
	scale 0.5
}

actor Lightrailer
{
	+MissileMore
	+LOOKALLAROUND
	obituary "%o got blinded a smart bomb!"
	reactiontime 25
	MinMissileChance 0
	damagetype "corefriendly"
	Radius 0
	Height 0
	States
	{
	Spawn:
		TNT1 A 0 A_Changeflag("FRIENDLY", 1)
		TNT1 A 1 A_Look
		TNT1 A 0 A_Changeflag("FRIENDLY", 0)
		TNT1 A 1 A_Look
		TNT1 A 0 A_Changeflag("FRIENDLY", 1)
		TNT1 A 1 A_Look
		TNT1 A 0 A_Changeflag("FRIENDLY", 0)
		TNT1 A 1 A_Look
		Stop
	See:
		TNT1 A 1 A_Facetarget
		TNT1 A 1 A_CustomRailgun (200, 0, NONE, WHITE, 1)
		TNT1 A 0 A_CustomRailgun (200, 0, NONE, GREY, 1)
		TNT1 A 0 A_CustomRailgun (200, 0, NONE, GREY, 1)
		TNT1 A 0 A_CustomRailgun (200, 0, NONE, WHITE, 1)
		TNT1 A 0 A_CustomRailgun (200, 0, NONE, GREY, 1)
		TNT1 A 0 A_CustomRailgun (200, 0, NONE, GREY, 1)
		TNT1 A 0 A_CustomRailgun (200, 0, NONE, WHITE, 1)
		TNT1 A 0 A_Countdown
		goto spawn
	}
}

actor RingLightemmit1
{
	+CLIENTSIDEONLY
	radius 0
	height 0
	Renderstyle ADD
	alpha 0.5
	scale 5.0
	States
	{
	Spawn:
		WHFL B 2 BRIGHT
		stop
	}
}

actor ringlightemmit2 : ringlightemmit1
{
	alpha 0.4
	scale 6.0
}

actor ringlightemmit3 : ringlightemmit1
{
	alpha 0.3
	scale 7.0
}

actor ringlightemmit4 : ringlightemmit1
{
	alpha 0.2
	scale 8.0
}

actor ringlightemmit5 : ringlightemmit1
{
	alpha 0.1
	scale 9.0
}

//////////////////////////////////////////////////////////////////////////////////////////////////////////////
// VAMPIRISM / DRAIN /////////////////////////////////////////////////////////////////////////////////////////
//////////////////////////////////////////////////////////////////////////////////////////////////////////////

actor DrainItem : PowerupGiver 24040
{
	//$Category Stronghold_Items
	scale 0.75
	inventory.pickupmessage "Picked up a vampirism sphere."
	inventory.icon "VAMIA0"
	inventory.maxamount 3
	inventory.usesound "pickups/slowmo"
	powerup.color "AA 00 00" 0.20
	powerup.type "Drain"
	powerup.duration 1575
	Tag "Vampirism"
	+INVENTORY.FANCYPICKUPSOUND
	states
	{
	Spawn:
		VAMP A -1 BRIGHT
		stop
	}
}

///////////////////////////////////////////////////////////////////////////////////////////////////////
// PROTECTION /////////////////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////////////////////////////

actor PowerQuarterDamage : PowerProtection
{
	damagefactor "normal", 0.25
	inventory.icon "MEGAA0"
}

actor ProtectionItem : PowerupGiver 24041
{
	//$Category Stronghold_Items
	scale 0.75
	inventory.pickupmessage "Picked up a protection sphere."
	inventory.icon "PROIA0"
	inventory.maxamount 3
	inventory.usesound "pickups/slowmo"
	powerup.color "FF AA 00" 0.10
	powerup.type QuarterDamage
	powerup.duration 1575
	Tag "Protection"
	+INVENTORY.FANCYPICKUPSOUND
	states
	{
	Spawn:
		PROP A -1 BRIGHT
		stop
	}
}

/////////////////////////////////////////////////////////////////////////////////////////////////////////
// MORPHPOWERUP /////////////////////////////////////////////////////////////////////////////////////////
/////////////////////////////////////////////////////////////////////////////////////////////////////////

ACTOR MorphPlayerItem : PowerUpGiver 24042
{
	//$Category Stronghold_Items
	scale 0.75
	Inventory.PickupMessage "Picked up a morph sphere."
	inventory.usesound "pickups/slowmo"
	inventory.maxamount 2
	inventory.icon "MORIA0"
	Powerup.Type MorphPlayer
	Powerup.Duration 1575
	Tag "Morpher"
	+INVENTORY.FANCYPICKUPSOUND
	states
	{
	Spawn:
		MORP A -1 BRIGHT
		stop
	}
}

ACTOR PowerMorphPlayer : PowerMorph
{
	PowerMorph.PlayerClass "MorphedPlayer"
	PowerMorph.MorphStyle (MRF_FULLHEALTH|MRF_ADDSTAMINA|MRF_WHENINVULNERABLE|MRF_NEWTIDBEHAVIOUR|MRF_LOSEACTUALWEAPON)
}

/////////////////////////////////////////////////////////////////////////////////////////////////
// I WIN BUTTON - POWERUP ///////////////////////////////////////////////////////////////////////
/////////////////////////////////////////////////////////////////////////////////////////////////

actor PowerOSPDamage : PowerDamage
{
	damagefactor "normal", 4
}
actor PowerOSPSpeed : PowerSpeed
{
	Speed 2.0
}

actor OSPSpeed : PowerupGiver
{
	+INVENTORY.AUTOACTIVATE
	+INVENTORY.ALWAYSPICKUP
	Inventory.MaxAmount 0
	Powerup.Duration 350
	Powerup.Type "OSPSpeed"
	States
	{
	Spawn:
		MEGA A -1
		Loop
	}
}
actor OSPInv : OSPSpeed
{
	Powerup.Type "Invulnerable"
	Powerup.Color GoldMap
}
actor OSPQuad : OSPSpeed
{
	Powerup.Type "OSPDamage"
	Powerup.color "FF 00 00",0.35
}
actor OSPPower : OSPSpeed
{
	Powerup.Type "WeaponLevel2"
}

actor OSPHealth : Health
{
	Inventory.Amount 100
	Inventory.MaxAmount 100
	States
	{
	Spawn:
		SOUL A -1
		Loop
	}
}

actor IWinButtonItem : CustomInventory 23475
{
	//$Category Stronghold_Items
	Inventory.Icon IWBIA0
	Inventory.PickupMessage "You got the I Win Button. Yay!"
	+INVENTORY.INVBAR
	Tag "I Win Button"
	states
	{
	Spawn:
		IWBP A -1 BRIGHT
		stop
	Use:
		TNT1 A 0 A_GiveInventory("OSPInv")
		TNT1 A 0 A_GiveInventory("OSPQuad")
		TNT1 A 0 A_GiveInventory("OSPSpeed")
		TNT1 A 0 A_GiveInventory("OSPPower")
		TNT1 A 0 A_GiveInventory("OSPHealth")
		stop
	}
}

//////////////////////////////////////////////////////////////////////////////////////
/////////////////////////////////////// POWERUP	///////////////////////////////////
//////////////////////////////////////////////////////////////////////////////////////

actor WeaponPowerItem : CustomInventory 25007
{
	//$Category Stronghold_Items
	inventory.pickupmessage "Picked up a weapon powerup kit."
	inventory.maxamount 3
	inventory.icon POWIA0
	tag "Weapon Powerup Kit"
	+INVENTORY.INVBAR
	scale 0.75
	states
	{
	Spawn:
		POWP A -1 BRIGHT
		stop
	Pickup:
		TNT1 A 1 A_PlaySound("pickup")
		stop
	Use:
		TNT1 A 1 A_GiveInventory("WeaponPowerUpGiver")
		stop
	}
}

actor WeaponPowerupGiver : PowerupGiver
{
	powerup.color "80 00 ff",0.1
	powerup.duration 2100 //60 seconds
	powerup.type WeaponLevel2
	+INVENTORY.ALWAYSPICKUP
	+INVENTORY.AUTOACTIVATE
	inventory.maxamount 0
}