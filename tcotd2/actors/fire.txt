Actor FireSpawner : SwitchableDecoration 32700
{
	//$Category Special Effects
	//$Title Fire Spawner (arg1 Size123)
	//$Color 12
	Height 40
	Radius 30
	+ClientSideOnly
	+NoClip
	+NoBlockMap
	+NoGravity
	+NoInteraction
	States
	{
	Spawn:
	Active:
		TNT1 A 0 A_JumpIf(Args[1] > 0,2)
		TNT1 A 0 A_PlaySound("SFX/FireStart",7)
		TNT1 A 0 A_JumpIf(Args[0] == 1,"Medium")
		TNT1 A 0 A_JumpIf(Args[0] > 1,"Large")
		TNT1 A 0 A_JumpIf(Args[1] > 0,2)
		TNT1 A 0 A_PlaySound("SFX/FireLoop1",6,1,1)
		TNT1 A 0 A_SpawnItemEx("SmallFlame")
		TNT1 A 0 A_Jump(256,1,3,4,5,6,7)
		TNT1 A 1 A_SpawnItemEx("Flame1A")
		TNT1 A 0 A_SpawnItemEx("Ember1")
		Loop
		TNT1 A 1 A_SpawnItemEx("Flame1B")
		Loop
		TNT1 A 1 A_SpawnItemEx("Flame2A")
		Loop
		TNT1 A 1 A_SpawnItemEx("Flame2B")
		Loop
		TNT1 A 1 A_SpawnItemEx("Flame3A")
		Loop
		TNT1 A 1 A_SpawnItemEx("Flame3B")
		Loop
	Medium:
		TNT1 A 0 A_JumpIf(Args[1] > 0,2)
		TNT1 A 0 A_PlaySound("SFX/FireLoop2",6,1,1)
		TNT1 A 0 A_SpawnItemEx("MediumFlame")
		TNT1 A 0 A_Jump(256,1,3,4,5,6,7)
		TNT1 A 1 A_SpawnItemEx("Flame4A")
		TNT1 A 0 A_SpawnItemEx("Ember2")
		Loop
		TNT1 A 1 A_SpawnItemEx("Flame4B")
		Loop
		TNT1 A 1 A_SpawnItemEx("Flame5A")
		Loop
		TNT1 A 1 A_SpawnItemEx("Flame5B")
		Loop
		TNT1 A 1 A_SpawnItemEx("Flame6A")
		Loop
		TNT1 A 1 A_SpawnItemEx("Flame6B")
		Loop
	Large:
		TNT1 A 0 A_JumpIf(Args[1] > 0,2)
		TNT1 A 0 A_PlaySound("SFX/FireLoop3",6,1,1)
		TNT1 A 0 A_SpawnItemEx("LargeFlame")
		TNT1 A 0 A_Jump(256,1,3,4,5,6,7)
		TNT1 A 1 A_SpawnItemEx("Flame7A")
		TNT1 A 0 A_SpawnItemEx("Ember2")
		Loop
		TNT1 A 1 A_SpawnItemEx("Flame7B")
		Loop
		TNT1 A 1 A_SpawnItemEx("Flame8A")
		Loop
		TNT1 A 1 A_SpawnItemEx("Flame8B")
		Loop
		TNT1 A 1 A_SpawnItemEx("Flame9A")
		Loop
		TNT1 A 1 A_SpawnItemEx("Flame9B")
		Loop
	Inactive:
		TNT1 A 0 A_JumpIf(Args[1] > 0,2)
		TNT1 A 0 A_PlaySound("SFX/FireDie",7)
		TNT1 A -1 A_StopSound(6)
		Stop
	}
}

//Dynamic Light actors
Actor SmallFlame
{
	+ClientSideOnly
	+NoClip
	+NoBlockMap
	+NoGravity
	+NoInteraction
	States
	{
	Spawn:
		TNT1 A 1
		Stop
	}
}

Actor MediumFlame : SmallFlame {}
Actor LargeFlame : SmallFlame {}

//Flames and particles
actor Flame1A
{
	height 2
	radius 1
	+NOGRAVITY
	+NOCLIP
	+NOBLOCKMAP
	+MISSILE
	+CLIENTSIDEONLY
	+NOINTERACTION
	+ForceXYBillboard
	RenderStyle Add
	Alpha 0.8
	Scale 0.35
	states
	{
	Spawn:
		FLM1 A 0
		"####" A 0 ThrustThingZ(0,random(2,6),0,0)
		"####" A 0 ThrustThing(random(0,255),random(0,1),0,0)
		"####" ABCDE 2 bright
		"####" FFGGHHIIJJKKLL 1 bright A_FadeOut(.05)
		stop
	}
}

actor Flame1B : Flame1A
{
	states
	{
	Spawn:
		FLM2 A 0
		Goto Super::Spawn+1
	}
}

actor Flame2A : Flame1A
{
	states
	{
	Spawn:
		FLM3 A 0
		Goto Super::Spawn+1
	}
}

actor Flame2B : Flame1A
{
	states
	{
	Spawn:
		FLM4 A 0
		Goto Super::Spawn+1
	}
}

actor Flame3A : Flame1A
{
	states
	{
	Spawn:
		FLM5 A 0
		Goto Super::Spawn+1
	}
}

actor Flame3B : Flame1A
{
	states
	{
	Spawn:
		FLM6 A 0
		Goto Super::Spawn+1
	}
}

Actor Flame4A : Flame1A
{
	Scale 0.7
	States
	{
	Spawn:
		FLM1 A 0
		"####" A 0 ThrustThingZ(0,Random(4,12),0,0)
		"####" A 0 ThrustThing(Random(0,255),Random(0,2),0,0)
		"####" ABCDE 2 Bright
		"####" FFGGHHIIJJKKLL 1 Bright A_FadeOut(.05)
		Stop
	}
}

Actor Flame4B : Flame4A
{
	States
	{
	Spawn:
		FLM2 A 0
		Goto Super::Spawn+1
	}
}

Actor Flame5A : Flame4A
{
	States
	{
	Spawn:
		FLM3 A 0
		Goto Super::Spawn+1
	}
}

Actor Flame5B : Flame4A
{
	States
	{
	Spawn:
		FLM4 A 0
		Goto Super::Spawn+1
	}
}

Actor Flame6A : Flame4A
{
	States
	{
	Spawn:
		FLM5 A 0
		Goto Super::Spawn+1
	}
}

Actor Flame6B : Flame4A
{
	States
	{
	Spawn:
		FLM6 A 0
		Goto Super::Spawn+1
	}
}

Actor Flame7A : Flame1A
{
	Scale 1.4
	States
	{
	Spawn:
		FLM1 A 0
		"####" A 0 ThrustThingZ(0,Random(8,24),0,0)
		"####" A 0 ThrustThing(Random(0,255),Random(0,4),0,0)
		"####" ABCDE 2 Bright
		"####" FFGGHHIIJJKKLL 1 Bright A_FadeOut(.05)
		Stop
	}
}

Actor Flame7B : Flame7A
{
	States
	{
	Spawn:
		FLM2 A 0
		Goto Super::Spawn+1
	}
}

Actor Flame8A : Flame7A
{
	States
	{
	Spawn:
		FLM3 A 0
		Goto Super::Spawn+1
	}
}

Actor Flame8B : Flame7A
{
	States
	{
	Spawn:
		FLM4 A 0
		Goto Super::Spawn+1
	}
}

Actor Flame9A : Flame7A
{
	States
	{
	Spawn:
		FLM5 A 0
		Goto Super::Spawn+1
	}
}

Actor Flame9B : Flame7A
{
	States
	{
	Spawn:
		FLM6 A 0
		Goto Super::Spawn+1
	}
}

actor Ember1 : Flame1A
{
	Scale .1
	states
	{
	Spawn:
		EMBR A 0
		EMBR A 0 ThrustThingZ(0,random(4,16),0,0)
		EMBR A 0 ThrustThing(random(0,255),random(0,1),0,0)
		EMBR A 32 Bright
	Death:
		EMBR A 2 Bright A_FadeOut
		loop
	}
}

Actor Ember2 : Ember1
{
	States
	{
	Spawn:
		EMBR A 0
		EMBR A 0 ThrustThingZ(0,random(4,16),0,0)
		EMBR A 0 ThrustThing(random(0,255),random(0,1),0,0)
		EMBR A 64 Bright
		Goto Death
	}
}