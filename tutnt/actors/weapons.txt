//TNT Fire Weapons By: Eric Ou
//===================================WEAPONS====================================
//Flamethrower
ACTOR Flamer : Weapon 20105
{
	Inventory.PickupMessage "You got the flamethrower! Time for a BBQ!"
	Obituary "%o was BBQed by %k's flamethrower."
	Weapon.AmmoType "Gas"
	Weapon.AmmoGive 48
	Weapon.AmmoUse 1
	+WEAPON.EXPLOSIVE
	Tag "Flamethrower"
	States
	{
	Spawn:
		WFLM A -1
		Loop
	Ready:
		FLMG BCD 3 A_WeaponReady
		Loop
	Deselect:
		FLMG A 1 A_Lower
		Loop
	Select:
		FLMG A 1 A_Raise
		Loop
	Fire:
		FLMG E 0 A_GunFlash
		FLMG E 1 Bright A_FireCustomMissile("FTFire",0,1,random(-4,4),0)
		FLMG F 1 Bright A_FireCustomMissile("FTFire",0,0,random(-4,4),0)
		FLMG G 1 Bright A_FireCustomMissile("FTFire",0,1,random(-4,4),0)
		FLMG E 0 A_GunFlash
		FLMG E 1 Bright A_FireCustomMissile("FTFire",0,1,random(-4,4),0)
		FLMG F 1 Bright A_FireCustomMissile("FTFire",0,0,random(-4,4),0)
		FLMG G 1 Bright A_FireCustomMissile("FTFire",0,1,random(-4,4),0)
		FLMG E 0 A_GunFlash
		FLMG E 1 Bright A_FireCustomMissile("FTFire",0,1,random(-4,4),0)
		FLMG F 1 Bright A_FireCustomMissile("FTFire",0,0,random(-4,4),0)
		FLMG G 1 Bright A_FireCustomMissile("FTFire",0,1,random(-4,4),0)
		FLMG H 2 Bright A_Refire
		Goto Ready
	Hold:
		FLMG E 0 A_GunFlash
		FLMG E 1 Bright A_FireCustomMissile("FTFire",0,1,random(-4,4),0)
		FLMG F 1 Bright A_FireCustomMissile("FTFire",0,0,random(-4,4),0)
		FLMG G 1 Bright A_FireCustomMissile("FTFire",0,1,random(-4,4),0)
		FLMG E 0 A_GunFlash
		FLMG E 1 Bright A_FireCustomMissile("FTFire",0,1,random(-4,4),0)
		FLMG F 1 Bright A_FireCustomMissile("FTFire",0,0,random(-4,4),0)
		FLMG G 1 Bright A_FireCustomMissile("FTFire",0,1,random(-4,4),0)
		FLMG E 0 A_GunFlash
		FLMG E 1 Bright A_FireCustomMissile("FTFire",0,1,random(-4,4),0)
		FLMG F 1 Bright A_FireCustomMissile("FTFire",0,0,random(-4,4),0)
		FLMG G 1 Bright A_FireCustomMissile("FTFire",0,1,random(-4,4),0)
		FLMG H 2 Bright A_Refire
		Goto Ready
	Flash:
		TNT1 A 2 A_Light1
		TNT1 A 2 A_Light2
		TNT1 A 1 A_Light1
		Goto LightDone
	}
}

ACTOR FTFire
{
	Radius 2
	Height 4
	Speed 16
	Damage 1
	PROJECTILE
	RENDERSTYLE ADD
	DamageType Fire
	ALPHA 0.67
	Scale 0.67
	SeeSound "weapons/flamer"
	DeathSound "weapons/scorch"
	Gravity .125
	States
	{
	Spawn:
		TNT1 A 2
		FRFX ABCD 2 Bright LIGHT("FlameThrowerFire") A_Explode(8,8,0)
		FRFX D 0 A_Gravity
		FRFX EFG 2 Bright LIGHT("FlameThrowerFire") A_Explode(8,16,0)
		FRFX HIJ 2 Bright LIGHT("FlameThrowerFire") A_Explode(8,32,0)
		Goto Death+4
	Death:
		FRFX HIJ 2 Bright LIGHT("FlameThrowerFire") A_Explode(8,32,0)
		FRFX J 0 A_CustomMissile("DropFire",0)
		FRFX KLM 2 Bright LIGHT("FlameThrowerFire") A_Explode(8,64,0)
		FRFX NO 2 Bright LIGHT("FlameThrowerFire")
		stop
	}
}

ACTOR DropFire
{
	Radius 8
	Height 40
	PROJECTILE
	RENDERSTYLE ADD
	DamageType Fire
	ALPHA 0.90
	-NOGRAVITY
	Gravity .125
	+NOEXPLODEFLOOR
	States
	{
	Spawn:
		FLME A 1 Bright LIGHT("Flame") A_Playsound("weapons/onfire")
		FLME A 1 Bright LIGHT("Flame") A_Explode(8,16)
		FLME BCDEFGHIJKLMN 2 Bright LIGHT("Flame") A_Explode(8,16,0)
		FLME A 1 Bright LIGHT("Flame") A_Playsound("weapons/onfire")
		FLME A 1 Bright LIGHT("Flame") A_Explode(8,32)
		FLME BCDEFGHIJKLMN 2 Bright LIGHT("Flame") A_Explode(8,16,0)
		FLME A 1 Bright LIGHT("Flame") A_Playsound("weapons/onfire")
		FLME A 1 Bright LIGHT("Flame") A_Explode(8,32)
		FLME BCDEFGHIJKLMN 2 Bright LIGHT("Flame") A_Explode(8,16,0)
		FLME A 1 Bright LIGHT("Flame") A_Playsound("weapons/onfire")
		FLME A 1 Bright LIGHT("Flame") A_Explode(8,32)
		FLME BCDEFGHIJKLMN 2 Bright LIGHT("Flame") A_Explode(8,16,0)
		FLME A 1 Bright LIGHT("Flame") A_Playsound("weapons/onfire")
		FLME A 1 Bright LIGHT("Flame") A_Explode(8,32)
		FLME BCDEFGHIJKLMN 2 Bright LIGHT("Flame") A_Explode(8,16,0)
		FLME A 0 A_Jump(192,1)
		loop
	Death:
		TNT1 A 0
		stop
	}
}

ACTOR Gas : Ammo 20106
{
	Inventory.Amount 24
	Inventory.MaxAmount 240
	Ammo.BackpackAmount 24
	Ammo.BackpackMaxAmount 480
	Inventory.PickupMessage "You picked up a small gasoline cannister."
	Inventory.Icon AGASA0
	States
	{
	Spawn:
		AGAS A -1
		Loop
	}
}

ACTOR BigGas : Gas 20195
{
	Inventory.Amount 120
	Inventory.PickupMessage "You picked up a gasoline Pack."
	Inventory.Icon AGASB0
	States
	{
	Spawn:
		AGAS B -1
		Loop
	}
}
//End of Flamer
//PyroCannon
ACTOR PyroCannon : Weapon 20107
{
	Inventory.PickupMessage "You got the PyroCannon ProtoType!"
	Obituary "%o was incinerated by %k's Pyrocannon."
	Weapon.AmmoType "Gas"
	Weapon.AmmoGive 96
	Weapon.AmmoUse 48
	+WEAPON.EXPLOSIVE
	+WEAPON.BFG
	+WEAPON.DONTBOB
	+WEAPON.STAFF2_KICKBACK
	States
	{
	Spawn:
		WPRY A -1
		Loop
	Ready:
		PYRG A 1 A_WeaponReady
		Loop
	Deselect:
		PYRG A 1 A_Lower
		Loop
	Select:
		PYRG A 1 A_Raise
		Loop
	Fire:
		PYRG A 5
		PYRG B 8 Bright A_GunFlash
		PYRG C 10 Bright A_FireCustomMissile("PyroShot")
		PYRG DEFG 5 Bright
		PYRG A 5 A_Refire
		Goto Ready
	Flash:
		TNT1 A 8 A_Light1
		TNT1 A 10 A_Light2
		TNT1 A 15 A_Light1
		Goto LightDone
	}
}

ACTOR PyroShot
{
	Radius 8
	Height 12
	Speed 30
	Damage 180
	PROJECTILE
	RENDERSTYLE ADD
	DamageType Fire
	ALPHA 0.95
	Scale 0.75
	SeeSound "weapons/hellfi"
	DeathSound "weapons/hellex"
	States
	{
	Spawn:
		PBAL ABA 4 Bright LIGHT("PyroShot")
		PBAL BA 4 Bright LIGHT("PyroShot") A_Explode
		goto Spawn+3
	Death:
		PYXP C 2 Bright LIGHT("PyroShot_1") A_Explode(192,128)
		TNT1 A 0 A_CustomMissile("PyroSpawner",0,0,0)
		TNT1 A 0 A_CustomMissile("PyroSpawner",0,0,45)
		TNT1 A 0 A_CustomMissile("PyroSpawner",0,0,90)
		TNT1 A 0 A_CustomMissile("PyroSpawner",0,0,135)
		TNT1 A 0 A_CustomMissile("PyroSpawner",0,0,180)
		TNT1 A 0 A_CustomMissile("PyroSpawner",0,0,225)
		TNT1 A 0 A_CustomMissile("PyroSpawner",0,0,270)
		TNT1 A 0 A_CustomMissile("PyroSpawner",0,0,315)
		TNT1 A 0 A_Mushroom("PyroFlare",16)
		PYXP D 2 BRIGHT LIGHT("PyroShot_2")
		PYXP E 2 BRIGHT LIGHT("PyroShot_3")
		PYXP F 2 BRIGHT LIGHT("PyroShot_4")
		PYXP G 2 BRIGHT LIGHT("PyroShot_5")
		PYXP H 2 BRIGHT LIGHT("PyroShot_6")
		PYXP I 2 BRIGHT LIGHT("PyroShot_7")
		PYXP J 2 BRIGHT LIGHT("PyroShot_8")
		PYXP K 2 BRIGHT LIGHT("PyroShot_9")
		PYXP L 2 BRIGHT LIGHT("PyroShot_10")
		PYXP M 2 BRIGHT LIGHT("PyroShot_11")
		PYXP N 2 BRIGHT LIGHT("PyroShot_12")
		PYXP O 2 BRIGHT LIGHT("PyroShot_13")
		PYXP P 2 BRIGHT LIGHT("PyroShot_14")
		PYXP Q 2 BRIGHT LIGHT("PyroShot_15")
		PYXP R 2 BRIGHT LIGHT("PyroShot_16")
		PYXP S 2 BRIGHT LIGHT("PyroShot_17")
		PYXP T 2 BRIGHT LIGHT("PyroShot_18")
		PYXP U 2 BRIGHT LIGHT("PyroShot_19")
		stop
	}
}

ACTOR PyroBoom
{
	Radius 0
	Height 0
	PROJECTILE
	RENDERSTYLE ADD
	DamageType Fire
	ALPHA 0.95
	Scale 0.67
	SeeSound "weapons/boom1"
	States
	{
	Spawn:
		PYXP AB 2 Bright
		PYXP C 2 Bright A_Explode(192,128)
		PYXP DEFGHIJKLMNOPQRSTU 2 Bright
		stop
	}
}

ACTOR PyroSpawner
{
	Radius 1
	Height 1
	Damage 10
	PROJECTILE
	Speed 15
	+RIPPER
	+BLOODLESSIMPACT
	States
	{
	Spawn:
		TNT1 AAAAA 5 A_CustomMissile("PyroBoom")
		stop
	}
}

ACTOR PyroFlare
{
	Radius 8
	Height 11
	Speed 35
	Damage 10
	PROJECTILE
	RENDERSTYLE ADD
	DamageType Fire
	ALPHA 0.95
	+ROCKETTRAIL
	-NOGRAVITY
	DeathSound "weapons/bang1"
	States
	{
	Spawn:
		TNT1 A 1 A_SpawnItemEx("PyroFX")
		loop
	Death:
		FRFX HIJ 2 Bright
		FRFX J 0 A_Explode(64)
		FRFX KLMNO 2 Bright
		stop
	}
}

ACTOR PyroFX
{
	+CLIENTSIDEONLY
	Radius 0
	Height 0
	PROJECTILE
	RENDERSTYLE ADD
	ALPHA 0.85
	States
	{
	Spawn:
		TNT1 A 3
		PYFX ABCDE 3 Bright
		Stop
	}
}

//End of PyroCannon
//============================END OF WEAPONS=================================

ACTOR NewPistol : Pistol replaces Pistol
{
	AttackSound "weapons/pistol"
	Decal BulletChip
	States
	{
	Fire:
		PISG A 2
		PISG A 1 A_SpawnItemEx("PisCaseSpawn",20,0,29)
		PISG B 0 A_GunFlash
		PISG B 6 A_FireBullets(4,4,1,5,"NewPuff",1)
		PISG C 4
		PISG B 5 A_Refire
		Goto Ready
	}
}

ACTOR PisCaseSpawn
{
	+CLIENTSIDEONLY
	PROJECTILE
	+NOCLIP
	States
	{
	Spawn:
		TNT1 A 1
		TNT1 A 1 A_SpawnItemEx("PistolCasing",0,0,0,4,0,Random(2,4),Random(85,95))
		Stop
	}
}

ACTOR PistolCasing
{
	+CLIENTSIDEONLY
	Height 8
	Radius 6
	Speed 4
	Scale 0.45
	BounceFactor 0.4
	Mass 1
	PROJECTILE
	BounceType Doom
	-NOGRAVITY
	-NOBLOCKMAP
	BounceSound "weapons/case1"
	States
	{
	Spawn:
		CAS2 ABCD 2
		Loop
	Death:
		CAS2 A 0 A_Jump(256,1,2,3,4)
		CAS2 ABCD 0 A_Jump(256,"End")
	End:
		"####" "#" 350
		Stop
	}
}

ACTOR NewShotgun : Shotgun replaces Shotgun
{
	AttackSound "weapons/shotgf"
	Decal BulletChip
	States
	{
	Fire:
		SHTG A 3
		SHTG A 0 A_GunFlash
		SHTG A 7 A_FireBullets(5.6,0,7,5,"NewPuff")
		SHTG BC 5
		SHTG D 1
		SHTG D 3 A_SpawnItemEx("ShotCaseSpawn",20,-4,24)
		SHTG CB 5
		SHTG A 3
		SHTG A 7 A_Refire
		Goto Ready
	}
}

ACTOR NewSuperShotgun : SuperShotgun replaces SuperShotgun
{
	AttackSound "weapons/sshotf"
	Decal BulletChip
	States
	{
	Fire:
		SHT2 A 3
		SHT2 A 0 A_GunFlash
		SHT2 A 7 A_FireBullets(12,6,20,5,"NewPuff")
		SHT2 B 7 A_CheckReload
		SHT2 C 7
		SHT2 D 0 A_SpawnItemEx("ShotCaseSpawn",20,-2,30,0,0,0,180)
		SHT2 D 0 A_SpawnItemEx("ShotCaseSpawn",20,2,30,0,0,0,180)
		SHT2 D 7 A_PlaySound("weapons/sshoto")
		SHT2 E 7
		SHT2 F 7 A_PlaySound("weapons/sshotl")
		SHT2 G 7
		SHT2 H 0 A_Refire
		SHT2 H 7 A_PlaySound("weapons/sshotc")
		Goto Ready
	}
}

ACTOR ShotCaseSpawn : PisCaseSpawn
{
	States
	{
	Spawn:
		TNT1 A 1
		TNT1 A 1 A_SpawnItemEx("ShotgunCasing",0,0,0,4,0,Random(2,4),Random(-95,-85))
		Stop
	}
}

ACTOR ShotgunCasing : PistolCasing
{
	Height 12
	Radius 9
	Scale 0.7
	BounceSound "weapons/shell"
	States
	{
	Spawn:
		D3S1 ACBHEGFD 1
		Loop
	Death:
		D3S1 A 0 A_Jump(256,1,2,3,4,5,6,7,8)
		D3S1 ABCDEFGH 0 A_Jump(256,"End")
	}
}

ACTOR NewChaingun : Chaingun replaces Chaingun
{
	attacksound "weapons/chngun"
	Decal BulletChip
	States
	{
	Fire:
		CHGG A 0 A_GunFlash
		CHGG A 0 A_SpawnItemEx("PisCaseSpawn",20,6,20)
		CHGG A 4 A_FireBullets(4,4,1,5,"NewPuff",1)
		CHGG A 0 A_CheckReload
		CHGG A 0 A_GunFlash("Flash2")
		CHGG B 0 A_SpawnItemEx("PisCaseSpawn",20,6,20)
		CHGG B 4 A_FireBullets(4,4,1,5,"NewPuff",1)
		CHGG B 0 A_Refire
		Goto Ready
	Flash:
		CHGF A 5 BRIGHT A_Light1
		Goto LightDone
	Flash2:
		CHGF B 5 BRIGHT A_Light2
		Goto LightDone
	}
}

ACTOR NewPuff : BulletPuff
{
	Alpha 1.0
	//Mass 12 //I like the fact puffs make big splashes.
	// But as long as there are terrain sounds for solid floors puffs must not do them!
	States
	{
	Spawn:
	Melee:
		PUFF A 2 LIGHT("BPUFF1")
		PUFF A 0 A_Jump(128,2)
		PUFF A 2 LIGHT("BPUFF1") A_SpawnItemEx("Puff2")
		PUFF B 4 LIGHT("BPUFF2")
		PUFF C 4 LIGHT("BPUFF3")
		PUFF D 4 LIGHT("BPUFF4")
		Stop
	}
}

ACTOR Puff2
{
	+CLIENTSIDEONLY
	PROJECTILE
	+NOCLIP
	Mass 1
	States
	{
	Spawn:
		PUFF E 2 Bright
		PUFF E 2 Bright A_PlaySound("puff/ric")
		PUFF FGH 4 Bright
		Stop
	}
}

//// Skulltag Minigun

ACTOR STMinigun : Weapon 20039
{
	Weapon.AmmoType "Clip"
	Weapon.AmmoGive 20
	Weapon.AmmoUse 1
	Inventory.PickupMessage "You got the minigun!"
	Obituary "%o was shredded by %k's minigun."
	AttackSound "weapons/minigunfire"
	Tag "Minigun"
	States
	{
	Spawn:
		MNGN A -1
		Loop
	Ready:
		MNGG A 1 A_WeaponReady
		Loop
	Deselect:
		MNGG A 1 A_Lower
		Loop
	Select:
		MNGG A 1 A_Raise
		Loop
	Fire:
		MNGG A 0 A_GunFlash
		MNGG A 0 A_SpawnItemEx("PisCaseSpawn",20,6,20)
		MNGG A 2 A_FireBullets(5,1,1,5,"NewPuff",1)
		MNGG B 0 A_SpawnItemEx("PisCaseSpawn",20,6,20)
		MNGG B 2 A_FireBullets(5,1,1,5,"NewPuff",1)
		MNGG B 2 A_ReFire
		MNGG A 5
		MNGG B 8
		MNGG A 11
		MNGG B 14
		MNGG A 17
		goto Ready
	Flash:
		MNGF A 3 BRIGHT A_Light1
		MNGF B 3 BRIGHT A_Light0
		Stop
	}
}

//////////////////////////////////
// WEAPON ENHANCEMENTS 4 BFG+PL //
//////////////////////////////////

Actor PSpark6_G1
{
	Speed 6
	Height 2
	Radius 1
	Mass 0
	+Missile
	Gravity .125
	RenderStyle Add
	Scale 0.12
	Translation "80:111=112:127","3:3=125:125"
	States
	{
	Spawn:
		SPRK A 64 Bright
	Death:
		SPRK B 1 Bright A_SetTranslucent(.8,1)
		SPRK CDEFGH 1 Bright A_FadeOut
		Stop
	}
}

Actor PSpark6_G2 : PSpark6_G1
{
	Scale 0.1
}

Actor PSpark6_G3 : PSpark6_G1
{
	Scale 0.08
	Speed 4
}

Actor PSpark6_G4 : PSpark6_G1
{
	Speed 4
}

Actor PSpark6_G5 : PSpark6_G1
{
	Speed 2
	Scale 0.1
}

Actor PSpark6_G6 : PSpark6_G1
{
	Speed 2
	Scale 0.08
}

Actor PSpark6_G7 : PSpark6_G1
{
	Speed 6
}

Actor PSpark6_G8 : PSpark6_G1
{
	Speed 4
	Scale 0.1
}

Actor PSpark6_G9 : PSpark6_G1
{
	Speed 2
	Scale 0.08
}

Actor PSpark6_Y1 : PSpark6_G1
{
	Speed 3
	Translation "80:97=225:227","98:98=160:160","99:103=228:231","104:111=161:167","3:3=161:161"
	States
	{
	Spawn:
		SPRK A 24 Bright
		Goto Death
	}
}

Actor PSpark6_Y2 : PSpark6_Y1
{
	Scale 0.1
}

Actor PSpark6_Y3 : PSpark6_Y1
{
	Scale 0.08
}

Actor PSpark6_Y4 : PSpark6_Y1
{
	Speed 2
}

Actor PSpark6_Y5 : PSpark6_Y1
{
	Speed 2
	Scale 0.1
}

Actor PSpark6_Y6 : PSpark6_y1
{
	Speed 2
	Scale 0.08
}

Actor PSpark6_Y7 : PSpark6_Y1
{
	Speed 1
}

Actor PSpark6_Y8 : PSpark6_Y1
{
	Speed 1
	Scale 0.1
}

Actor PSpark6_Y9 : PSpark6_Y1
{
	Speed 1
	Scale 0.08
}

Actor PSpark6_B1 : PSpark6_Y1
{
	Translation "80:104=192:204","205:211=240:246","3:3=206:206"
}

Actor PSpark6_B2 : PSpark6_B1
{
	Scale 0.1
}

Actor PSpark6_B3 : PSpark6_B1
{
	Scale 0.08
}

Actor PSpark6_B4 : PSpark6_B1
{
	Speed 2
}

Actor PSpark6_B5 : PSpark6_B1
{
	Speed 2
	Scale 0.1
}

Actor PSpark6_B6 : PSpark6_B1
{
	Speed 2
	Scale 0.08
}

Actor PSpark6_B7 : PSpark6_B1
{
	Speed 1
}

Actor PSpark6_B8 : PSpark6_B1
{
	Speed 1
	Scale 0.1
}

Actor PSpark6_B9 : PSpark6_B1
{
	Speed 1
	Scale 0.08
}

ACTOR PlasmaBallEnh : PlasmaBall replaces Plasmaball
{
	states
	{
	Spawn:
		PLSS AB 6 Bright LIGHT("Plasma")
		Loop
	Death:
		PLSE A 4 bright LIGHT("PlasmaX_1") A_SpawnItemEx("PSpark6SpawnerB")
		PLSE B 4 bright LIGHT("PlasmaX_2")
		PLSE C 4 bright LIGHT("PlasmaX_3")
		PLSE D 4 bright LIGHT("PlasmaX_4")
		PLSE E 4 bright LIGHT("PlasmaX_5")
		stop
	}
}

Actor PSpark6SpawnerB
{
	+CLIENTSIDEONLY
	States
	{
	Spawn:
		TNT1 A 0 NODELAY A_CustomMissile("PSpark6_B1",0,0,random(0,360),2,random(-158,-22))
		TNT1 A 0 A_CustomMissile("PSpark6_B2",0,0,random(0,360),2,random(-158,-22))
		TNT1 A 0 A_CustomMissile("PSpark6_B3",0,0,random(0,360),2,random(-158,-22))
		TNT1 A 0 A_CustomMissile("PSpark6_B4",0,0,random(0,360),2,random(-158,-22))
		TNT1 A 0 A_CustomMissile("PSpark6_B5",0,0,random(0,360),2,random(-158,-22))
		TNT1 A 0 A_CustomMissile("PSpark6_B6",0,0,random(0,360),2,random(-158,-22))
		TNT1 A 0 A_CustomMissile("PSpark6_B7",0,0,random(0,360),2,random(-158,-22))
		TNT1 A 0 A_CustomMissile("PSpark6_B8",0,0,random(0,360),2,random(-158,-22))
		TNT1 A 1 A_CustomMissile("PSpark6_B9",0,0,random(0,360),2,random(-158,-22))
		Stop
	}
}

ACTOR BFGBallEnh : BFGBall replaces BFGBall
{
	states
	{
	Spawn:
		BFS1 AB 4 Bright LIGHT("BFG")
		Loop
	Death:
		BFE1 A 8 bright LIGHT("BFGX_1") A_SpawnItemEx("PSpark6SpawnerG")
		BFE1 B 8 bright LIGHT("BFGX_2")
		BFE1 C 8 bright LIGHT("BFGX_3") A_BFGSpray
		BFE1 D 8 bright LIGHT("BFGX_4")
		BFE1 E 8 bright LIGHT("BFGX_5")
		BFE1 F 8 bright LIGHT("BFGX_6")
		stop
	}
}

Actor PSpark6SpawnerG : PSpark6SpawnerB
{
	States
	{
	Spawn:
		TNT1 AAA 0 NODELAY A_CustomMissile("PSpark6_G1",0,0,random(0,360),2,random(0,360))
		TNT1 AAA 0 A_CustomMissile("PSpark6_G2",0,0,random(0,360),2,random(0,360))
		TNT1 AAA 0 A_CustomMissile("PSpark6_G3",0,0,random(0,360),2,random(0,360))
		TNT1 AAA 0 A_CustomMissile("PSpark6_G4",0,0,random(0,360),2,random(0,360))
		TNT1 AAA 0 A_CustomMissile("PSpark6_G5",0,0,random(0,360),2,random(0,360))
		TNT1 AAA 0 A_CustomMissile("PSpark6_G6",0,0,random(0,360),2,random(0,360))
		TNT1 AAA 0 A_CustomMissile("PSpark6_G7",0,0,random(0,360),2,random(0,360))
		TNT1 AAA 0 A_CustomMissile("PSpark6_G8",0,0,random(0,360),2,random(0,360))
		TNT1 AAA 0 A_CustomMissile("PSpark6_G9",0,0,random(0,360),2,random(0,360))
		Stop
	}
}

Actor PSpark6SpawnerY : PSpark6SpawnerB
{
	States
	{
	Spawn:
		TNT1 A 0 NODELAY A_CustomMissile("PSpark6_Y1",0,0,random(0,360),2,random(-158,-22))
		TNT1 A 0 A_CustomMissile("PSpark6_Y2",0,0,random(0,360),2,random(-158,-22))
		TNT1 A 0 A_CustomMissile("PSpark6_Y3",0,0,random(0,360),2,random(-158,-22))
		TNT1 A 0 A_CustomMissile("PSpark6_Y4",0,0,random(0,360),2,random(-158,-22))
		TNT1 A 0 A_CustomMissile("PSpark6_Y5",0,0,random(0,360),2,random(-158,-22))
		TNT1 A 0 A_CustomMissile("PSpark6_Y6",0,0,random(0,360),2,random(-158,-22))
		TNT1 A 0 A_CustomMissile("PSpark6_Y7",0,0,random(0,360),2,random(-158,-22))
		TNT1 A 0 A_CustomMissile("PSpark6_Y8",0,0,random(0,360),2,random(-158,-22))
		TNT1 A 1 A_CustomMissile("PSpark6_Y9",0,0,random(0,360),2,random(-158,-22))
		Stop
	}
}